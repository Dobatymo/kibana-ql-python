start = Space* OrQuery? OptionalSpace
OrQuery = (AndQuery (Or AndQuery)+) / AndQuery
AndQuery = (NotQuery (And NotQuery)+) / NotQuery
NotQuery = (Not SubQuery) / SubQuery
SubQuery = ('(' Space* OrQuery OptionalSpace ')') / NestedQuery
NestedQuery = (Field Space* ':' Space* '{' Space* OrQuery OptionalSpace '}') / Expression
Expression = FieldRangeExpression / FieldValueExpression / ValueExpression
Field = Literal
FieldRangeExpression = Field Space* RangeOperator Space* Literal
FieldValueExpression = Field Space* ':' Space* ListOfValues
ValueExpression = Value
ListOfValues = ('(' Space* OrListOfValues OptionalSpace ')') / Value
OrListOfValues = (AndListOfValues (Or AndListOfValues)+) / AndListOfValues
AndListOfValues = (NotListOfValues (And NotListOfValues)+) / NotListOfValues
NotListOfValues = (Not ListOfValues) / ListOfValues
Value = QuotedString / UnquotedLiteral
Or = Space+ ~r'or'i Space+
And = Space+ ~r'and'i Space+
Not = ~r'not'i Space+
Literal = QuotedString / UnquotedLiteral
QuotedString = '"' QuotedCharacter* '"'
QuotedCharacter = EscapedWhitespace / EscapedUnicodeSequence / ('\\' ~r'[\\"]') / ~r'[^"]'
UnquotedLiteral = UnquotedCharacter+
UnquotedCharacter = EscapedWhitespace / EscapedSpecialCharacter / EscapedUnicodeSequence / EscapedKeyword / Wildcard / (!SpecialCharacter !Keyword ~r".")
Wildcard = '*'
OptionalSpace = Space*
EscapedWhitespace = '\\t' / '\\r' / '\\n'
EscapedSpecialCharacter = '\\' SpecialCharacter
EscapedKeyword = '\\' (~r'or'i / ~r'and'i / ~r'not'i)
Keyword = Or / And / Not
SpecialCharacter = ~r'[\\():<>"*{}]'
EscapedUnicodeSequence = '\\' UnicodeSequence
UnicodeSequence = "u" HexDigit HexDigit HexDigit HexDigit
HexDigit = ~r"[0-9a-f]"i
RangeOperator = '<=' / '>=' / '<' / '>'
Space = ~r"[ \t\r\n\u00A0]"u
